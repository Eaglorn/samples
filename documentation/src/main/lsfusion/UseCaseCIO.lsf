MODULE UseCaseCIO;

//#sample1
CLASS Color 'Color' {
    white 'White',
    black 'Black'
}
  
CLASS Book 'Book';
  
color 'Color' = DATA Color (Book);
//#sample1 end

//#solution1
// Option 1
nameColor1 'Color' (Book b) = staticCaption(color(b));
  
// Option 2
nameColor2 'Color' (Book b) = IF color(b) == Color.white THEN 'White' ELSE 'Black';
//#solution1 end

//#sample2
CLASS Order 'Order';
  
sent 'Sent' = DATA BOOLEAN (Order);
agreed 'Agreed' = DATA BOOLEAN (Order);
accepted 'Accepted' = DATA BOOLEAN (Order);
//#sample2 end

//#solution2
// Option 1
nameStatus1 'Status' (Order o) = CASE WHEN accepted(o) THEN 'Accepted'
                                     WHEN agreed(o) THEN 'Agreed'
                                     WHEN sent(o) THEN 'Sent'
                                ELSE 'New';
  
// Option 2
CLASS Status 'Status' {
    new 'New',
    sent 'Sent',
    agreed 'Agreed',
    accepted 'Accepted'
}
  
status 'Status' (Order o) = CASE WHEN accepted(o) THEN Status.accepted
                                 WHEN agreed(o) THEN Status.agreed
                                 WHEN sent(o) THEN Status.sent
                            ELSE Status.new;
nameStatus2 'Status' (Order o) = staticCaption(status(o));
//#solution2 end

//#solution3
dataMarkup 'Markup, %' = DATA NUMERIC[6,2] (Book);
 
defaultMarkup 'Default markup' = DATA NUMERIC[6,2] ();
 
markup1 'Markup, %' (Book b) = OVERRIDE dataMarkup(b), defaultMarkup();
 
// Equivalent to:
markup2 'Markup, %' (Book b) = IF dataMarkup(b) THEN dataMarkup(b) ELSE defaultMarkup();
//#solution3 end

//#sample4
CLASS Category 'Category';

category 'Category' = DATA Category (Book);
//#sample4 end

//#solution4
markup 'Markup, %' = DATA NUMERIC[6,2] (Category);
 
markup 'Markup, %' (Book b) = OVERRIDE dataMarkup(b), markup(category(b));
//#solution4 end

//#sample5
number 'Number' = DATA INTEGER (Book);
//#sample5 end

//#solution5
freeNumber1 () = (GROUP MAX number(Book b)) (+) 1;
  
// Equivalent to:
freeNumber2() = (OVERRIDE 0, (GROUP MAX number(Book b))) + 1;
//#solution5 end